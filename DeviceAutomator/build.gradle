plugins {
    id 'com.android.library'
    id 'de.marcphilipp.nexus-publish'
    id 'signing'
}

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.2'

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode rootProject.versionCode
        versionName rootProject.versionName

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    api 'androidx.test.uiautomator:uiautomator:2.2.0'
    api 'androidx.test:runner:1.3.0'
    api 'androidx.test:rules:1.3.0'
    api 'androidx.core:core:1.3.1'
    api 'androidx.test.ext:junit:1.1.2'

    androidTestImplementation 'com.google.dexmaker:dexmaker-mockito:1.2'

    testImplementation 'org.robolectric:robolectric:4.3'
    testImplementation 'org.mockito:mockito-core:2.28.2'
}

// region signing and publishing

task javadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocsJar(type: Jar, dependsOn: javadocs) {
    archiveClassifier.set('javadoc')
    from javadocs.destinationDir
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives javadocsJar
    archives sourcesJar
}

signing {
    required {
        !version.endsWith("SNAPSHOT") && !version.endsWith("DEVELOPMENT")
    }
    sign publishing.publications
}

nexusPublishing {
    // give nexus sonatype more time to initialize the staging repository
    clientTimeout = Duration.ofMinutes(3)
    repositories {
        sonatype()
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                groupId = 'com.braintreepayments'
                artifactId = 'device-automator'
                version = rootProject.versionName

                pom {
                    name = 'device-automator'
                    packaging = 'aar'
                    description = 'An easy to use, Espresso like, syntax on top of the Android UI Automator testing framework'
                    url = 'https://github.com/braintree/device-automator'

                    scm {
                        url = 'scm:git@github.com:braintree/device-automator.git'
                        connection = 'scm:git@github.com:braintree/device-automator.git'
                        developerConnection = 'scm:git@github.com:braintree/device-automator.git'
                    }

                    developers {
                        developer {
                            id = 'devs'
                            name = 'Braintree Payments'
                        }
                    }

                    licenses {
                        license {
                            name = 'MIT'
                            url = 'http://opensource.org/licenses/MIT'
                            distribution = 'repo'
                        }
                    }
                }
            }
        }
    }
}

// endregion
